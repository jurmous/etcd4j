plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'idea'
    id 'osgi'
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

group = 'org.mousio'
version = '2.8.0'

project.ext {
    isSnapshot = version.endsWith("-SNAPSHOT")
    isRelease = !isSnapshot

    mavenDeployUrl = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

    nettyVersion   = '4.1.0.Beta7'
    jacksonVersion = '2.6.3'
    slf4jVersion   = '1.7.12'
    junitVersion   = '4.12'

    if(!project.hasProperty('sonatypeUsername') && !project.hasProperty('sonatypePassword')) {
        sonatypeUsername = "$System.env.CI_DEPLOY_USERNAME"
        sonatypePassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "io.netty:netty-codec-http:$nettyVersion"
    compile "io.netty:netty-handler:$nettyVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
}

jar {
    baseName = "${project.name}"

    manifest.attributes['Bundle-Description'     ] = "${project.name}"
    manifest.attributes['Bundle-Name'            ] = "${project.name}"
    manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.name}"
    manifest.attributes['Bundle-Version'         ] = project.version.replaceAll('-','.')
    manifest.attributes['Implementation-Title'   ] = project.name
    manifest.attributes['Implementation-Version' ] = project.version
    manifest.attributes['Implementation-Vendor'  ] = 'jurmous'
}

signing {
    required {
        isRelease && gradle.taskGraph.hasTask("uploadArchives")
    }

    sign configurations.archives
}

/*
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
    options.compilerArgs << "-Xlint:uncheked"
}
*/

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: mavenDeployUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'etcd4j'
                packaging 'jar'
                description 'etcd4j is a Java/Netty based etcd client'
                url 'https://github.com/jurmous/etcd4j'

                scm {
                    url 'scm:git@github.com:jurmous/etcd4j.git'
                    connection 'scm:git@github.com:jurmous/etcd4j.git'
                    developerConnection 'scm:git@github.com:jurmous/etcd4j.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jurmous'
                        name 'Jurriaan Mous'
                    }
                    developer {
                        id 'lburgazzoli'
                        name 'Luca Burgazzoli'
                    }
                }
            }
        }
    }
}
