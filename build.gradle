plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'osgi'
    id 'eclipse'
    id 'idea'
    id 'org.unbroken-dome.test-sets' version '1.3.2'
    id "com.bmuschko.vagrant" version '2.1'
    id "com.github.lburgazzoli.karaf" version '0.0.44'
    id 'net.researchgate.release' version '2.5.0'
    id 'com.github.ben-manes.versions' version '0.14.0'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

group = 'org.mousio'

ext {
    isSnapshot = version.endsWith("-SNAPSHOT")
    isRelease  = !isSnapshot
    isCI       = Boolean.valueOf("$System.env.CI")
    isPR       = "false" != System.env.TRAVIS_PULL_REQUEST
    ciTasks    = (isRelease || isPR) ? ['clean', 'test'] : ['clean', 'test', 'uploadArchives']

    mavenDeployUrl = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

    versions = [
        netty   : '4.1.11.Final',
        jackson : '2.8.8',
        slf4j   : '1.7.23',
        junit   : '4.12',
        assertj : '3.8.0',
        restito : '0.9.0',
    ]

    osgiVersions = [
        slf4j   : "[1.7,1.8)",
        jackson : "[2.8,3.0)"
    ]

    if(!project.hasProperty('sonatypeUsername') && !project.hasProperty('sonatypePassword')) {
        sonatypeUsername = "$System.env.CI_DEPLOY_USERNAME"
        sonatypePassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

testSets {
    integration
}

configurations {
    karaf {
        extendsFrom compile
        exclude group: 'org.slf4j'
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:$versions.slf4j"
    compile "io.netty:netty-codec-dns:$versions.netty"
    compile "io.netty:netty-codec-http:$versions.netty"
    compile "io.netty:netty-handler:$versions.netty"
    compile "io.netty:netty-resolver-dns:$versions.netty"
    compile "com.fasterxml.jackson.core:jackson-core:$versions.jackson"
    compile "com.fasterxml.jackson.core:jackson-databind:$versions.jackson"
    compile "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:$versions.jackson"

    testCompile "junit:junit:$versions.junit"
    testCompile "org.slf4j:slf4j-simple:$versions.slf4j"
    testCompile "com.xebialabs.restito:restito:$versions.restito"
    testCompile "org.assertj:assertj-core:$versions.assertj"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test { 
    testLogging { 
        events "started", "passed", "skipped", "failed", "standardOut", "standardError" 
    }

    systemProperty 'java.net.preferIPv4Stack' , 'true'
}

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Bundle-Description'     ] = "${project.name}"
        attributes['Bundle-Name'            ] = "${project.name}"
        attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.name}"
        attributes['Bundle-Version'         ] = project.version.replaceAll('-','.')
        attributes['Implementation-Title'   ] = project.name
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'jurmous'

        // Tweak the OSGi imports so:
        // - slf4j do not define a specific import version
        // - add missing imports for Jackson Afterburner
        instruction 'Import-Package' , """
            org.slf4j;version=\"$osgiVersions.slf4j\",
            com.fasterxml.jackson.databind.ser;version=\"$osgiVersions.jackson\",
            com.fasterxml.jackson.databind.deser;version=\"$osgiVersions.jackson\",
            com.fasterxml.jackson.databind.ser.std;version=\"$osgiVersions.jackson\",
            com.fasterxml.jackson.databind.deser.std;version=\"$osgiVersions.jackson\",
            com.fasterxml.jackson.module.afterburner.ser;version=\"$osgiVersions.jackson\",
            com.fasterxml.jackson.module.afterburner.deser;version=\"$osgiVersions.jackson\",
            *
        """
    }
}

karaf {
    features {
        includeProject = true
        feature {
            name = 'etcd4j'
            configurations 'karaf'
        }
    }
}

signing {
    required {
        isRelease && gradle.taskGraph.hasTask("uploadArchives")
    }

    sign configurations.archives
}

// *****************************************************************************
// Publish to Sonatype OSS
// *****************************************************************************

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: mavenDeployUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'etcd4j'
                packaging 'jar'
                description 'etcd4j is a Java/Netty based etcd client'
                url 'https://github.com/jurmous/etcd4j'

                scm {
                    url 'scm:git@github.com:jurmous/etcd4j.git'
                    connection 'scm:git@github.com:jurmous/etcd4j.git'
                    developerConnection 'scm:git@github.com:jurmous/etcd4j.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jurmous'
                        name 'Jurriaan Mous'
                    }
                    developer {
                        id 'lburgazzoli'
                        name 'Luca Burgazzoli'
                    }
                }
            }
        }
    }
}

release {
    tagTemplate = 'release-${version}'
}

project.integration {
    dependsOn vagrantUp
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task ci(dependsOn: project.ciTasks) {
}

task close(dependsOn: 'closeAndPromoteRepository') {
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn uploadArchives
